plugins {
    id 'signing'
    id "java-library"
    id "maven-publish"
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

/* ------- */

sourceCompatibility = JavaVersion.VERSION_11

version = '1.0.0'

ext {
    projectDescription = 'A collection of new Skins for existing JavaFX controls. These Skins will add more functionality ' +
            'to the controls used in your applications with almost no changes to the applicationâ€™s code.'

    licenseName = 'GNU General Public License, version 2, with the Classpath Exception'
    licenseURL = 'https://openjdk.java.net/legal/gplv2+ce.html'

    versionControlOriginURL = 'https://github.com/dukke/FXSkins.git'
    issueTrackerURL = 'https://github.com/dukke/FXSkins/issues'

    projectURL = 'https://pixelduke.com/fxskins/'
}

/* ------- */

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls' ]
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    implementation 'org.controlsfx:controlsfx:11.0.0'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes( 'Implementation-Title':     project.name,
                    'Implementation-Version':   project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    options.addStringOption('javafx', '-quiet')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

/* Publishing */
/* - for publishing to maven central need to define MAVEN_USERNAME and MAVEN_PASSWORD as environment variables */
publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            description projectDescription

            name project.name
            url projectURL
            issueManagement {
                system 'GitHub'
                url issueTrackerURL
            }
            licenses {
                license {
                    name licenseName
                    url licenseURL
                    distribution 'repo'
                }
            }
            scm {
                url versionControlOriginURL
            }
            developers {
                developer {
                    name 'Pedro Duque Vieira'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}